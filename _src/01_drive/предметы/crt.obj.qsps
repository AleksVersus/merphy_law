QSP-Game Локация создаёт объект в игре
# crt.obj
$args[9]=$args[9]	& !@ инициализация аргументов
if args[4] = 0: args[4] = 1	& !@ если количество не указано, создаётся один предмет
if $args[0]<>'':
!@ если короткое имя задано, получаем предмет из базы
	!@ в зависимости от класса объекта, обращаемся к соответствующей базе
	if instr($args[2],'<ratch>')<>0:
		$args[2] = $func('base.ratch',$args[0],$args[2])
	elseif instr($args[2],'<knew>')<>0:
		$args[2] = $func('base.knew',$args[0],$args[2])	
	elseif instr($args[2],'<body>')<>0:
		$args[2] = $func('base.body',$args[0],$args[2])
	else
		$args[2] = $func('base.obj',$args[0],$args[2])
	end
else
!@ если короткое имя не задано,предмет генерируется на основе информации, переданной в $args[2]
	$args[2] = $args[2]
end
!@ если предпочтительный ай-ди не задан, ай-ди выбирается случайным образом
if $args[1]='': $id_array[]=$func('#rndstr#',8,'буквы и цифры') else $id_array[]=$args[1]
!@ создаём полное описание предмета
$object_array[] = $args[2]
!@ располагаем предмет в указанной позиции. если позиция не указана - на текущей локации.
if $args[5]<>'':$position_array[] = $args[5] else $position_array[] = $curloc
!@ задаём количество и заряд
charge_array[] = args[3]
kolvo_array[] = args[4]
!@ задаём динамический код предмета, выполняемый в нужных местах:
$run_array[] = $args[6]
$result = $id_array[]
--- crt.obj ---------------------------------
АРГУМЕНТ
$args[0] = $args[0]	&	!	короткое имя предмета (если не указано, используется полное описание
$args[1] = $args[1]	&	!	предпочтительный ай-ди (если не указано генерируется автоматически)
$args[2] = $args[2]	&	!	полное описание предмета (условные обозначения, описывающие предмет)
args[3] = args[3]	&	!	заряд
args[4]	&	!	количество
$args[5] = $args[5]	&	!	идентификатор места расположения
$args[6] = $args[6]	&	!	динамический код предмета, выполняемый в нужных местах
!"Создаётся строка в таблице:
$id_array[] - идентификатор предмета
$object_array[] - полное описание предмета
$position_array[] - расположение предмета
charge_array[] - заряд предмета
kolvo_array[] - количество предмета"