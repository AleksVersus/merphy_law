QSP-Game служебная локация для обработки перехода на новую локацию
Фактически здесь исходный текст книжки превращается в выводимый на экран.
# onNewLoc
!@ ---------------------------------- переход на новую локацию --------------------------------
	!@ данный скрипт срабатывает при условии перехода на абсолютно новую локацию
	if $id_watch[]<>$curloc:
		$id_watch[] = $curloc	& !@ записываем идентификатор локации в массив
		killvar '$tvar' & !@ очищаем временный массив:
		$GAME_VALUE['screen.floor']=''	& !@ выставляем уровень экрана (просматриваем локацию)
		if $GAME_VALUE['back.code']<>'back':
			!@ если переход на новую локацию не является возвратом по книге назад
			gosub 'save.varriors'	& !@ сохраняем данные
		else
			!@ если переход на новую локацию является откатом назад
			gosub 'back.varriors','back'	& !@ откатываем данные назад
		end
	end
	if arrsize('$id_watch')>100: killvar '$id_watch',0	& !@ удаляем ходы, если их больше ста
!@ ---------------------------------- переход на новую локацию --------------------------------
!@
!@ ---------------------------------- переход на любую локацию --------------------------------
	$refresh_watch[]=$curloc	& !@ записываем айди-локации при переходе на любую
	if arrsize('$refresh_watch')>9999: killvar '$refresh_watch',0	& !@ удаляем ходы, если больше 9999
!@ ---------------------------------- переход на любую локацию --------------------------------
!@
if val($strfind($curloc,'\d+'))>0 and strcomp($curloc,'page\.\d+')<>0:
	!@ если мы имеем дело с локацией, являющейся именно страницей книги-игры
	if $AVS_PLAYERTYPE['type']="qSpider" and $GAME_HELP['QN']='':
		!@ для Quest Navigator выводим подсказку по сохранениям
		$GAME_HELP['QN']='yep'
		$print[] = '<span style="color:#880000;">Внимание!!! Контрольные точки сохраняются в первом слоте, быстрые сохранения - во втором.</span> <br>'
	end
end
if здесь_был[$curloc]=0:
	здесь_был[$curloc]=1	& !@ локация посещена хотя бы раз
end
if $lvar['исходник']<>'':
	!@ если у нас есть исходник, то
	$args['на экран']=$func('int.DIN',$lvar['исходник'])	& !@ интерпретируем его и добавляем к выводу на экран
	$args['список предметов']=$func('int.loc.obj',$curloc)	& !@ интерпретируем список предметов, сброшенных на локацию
else
	!@ если у нас нет исходника, добавляем к выводу на экран то, что уже выведено на экран
	$args['на экран']='<avs-main>'+$maintxt+'</avs-main>'
end
$tvar['костыль.заголовок']=$lvar['заголовок']	& !@ подкостыливаем заголовок (не знаю, зачем я это сделал) [костыль 2]
!@ если на локации присутствует противник, добавляем ссылку на открытие боя:
if instr($lvar['заголовок'],'<avs-enemy>')<>0:$args['на экран']+=$func('base.txt','war')
!@ если на локации можно вести торговлю, добавляем подсказку о том, как торговать:
if instr($lvar['заголовок'],'<avs-money:')<>0: $args['на экран']+=$func('base.txt','sell')
!@ если это локация смерти, или включился режим проигрыша [герой потерял всё здоровье]
if instr($lvar['заголовок'],'<avs-death>')<>0 or $GAME_VALUE['power.lose']='poweroff':
	!@ добавляем оповещение, что силы покинули героя, если герой потерял здоровье
	if $GAME_VALUE['power.lose']='poweroff': $args['на экран']+=$func('base.txt','off')
	$args['на экран']+=$func('base.txt','wl')	& !@ добавляем ссылку в начало игры
	$args['список предметов']=''	& !@ убираем предметы с локации
	$GAME_INTERFACE['refresh']='страница.сброс'	& !@ принудительное обновление настроек экрана
	$GAME_VALUE['interface']=''	& !@ отключаем вывод статистики и предметов на экран
	nosave=1	& !@ отключаем возможность сохранений
else
	nosave=0	& !@ в противном случае включаем возможность сохранений
end
if instr($lvar['заголовок'],'<avs-victory>')<>0:
	!@ если это локация победы:
	$args['на экран']+=$func('base.txt','go')	& !@ выводим ссылку с оповещением о победе
	$args['список предметов']=''	& !@ убираем предметы с локации
	$GAME_INTERFACE['refresh']='страница.сброс'	& !@ принудительное обновление настроек экрана
	$GAME_VALUE['interface']=''	& !@ отключаем вывод статистики и предметов на экран
end
if instr($lvar['заголовок'],'<avs-game:')<>0:
	!@ если это локация с мини-игрой, получаем ссылку, разрешающую или прерывающую миниигру
	$args['на экран']+=$func('int.DIN',$func('base.txt','game.enemy',$func('get.tag.cont',$lvar['заголовок'],'avs-game')))
end
$args['на экран']+=$args['controlJS'] & !@ кусок костыля для прокрутки текста для Quest Navigator
*clr	& !@ очищаем окно основного описания
if $GAME_INTERFACE['refresh']<>'': gosub 'set.Screen',$GAME_INTERFACE['refresh']	& !@ если проставились настройки, обновляем экран
$args['PRINTSCREEN']=$func('int.screen',$args['на экран'],$args['список предметов'],$lvar['заголовок'],$GAME_VALUE['interface'],$lvar['колонтитул'])	& !@ скармливаем интерпретатору экрана все данные для формирования выводимого на экран текста
*pl $args['PRINTSCREEN']	& !@ выводим полученный кадр на экран
if $refresh_watch[]=$refresh_watch[arrsize('$refresh_watch')-2] and time['goto']=0:
	!@ при переходе на ту же самую локацию, проматываем экран книзу костылём
	wait 1
	*p '&nbsp;'
end
if $refresh_watch[]<>$refresh_watch[arrsize('$refresh_watch')-2]:
!@ если локация, на которую мы переместились, новая
	if instr($tvar['костыль.заголовок'],'<avs-enemy>')<>0:
		!@ если это локация с противником, приём лекарства запрещён
		$GAME_VALUE['приём лекарства']='запрещён'
	elseif $GAME_VALUE['приём лекарства']='запрещён':
		!@ сразу после битвы можно употребить лекарство
		$GAME_VALUE['приём лекарства']='разрешён'
	else
		!@ если не употребили лекарство сразу после битвы, запрещаем его употребление
		$GAME_VALUE['приём лекарства']=''
	end
end
!@ киллварим временные переменные
killvar '$lvar'
killvar '$GAME_WAR'
killvar '$GAME_WAR_LOG'
if instr($tvar['костыль.заголовок'],'<checkpoint>')<>0 and checkpoint[$curloc]=0:
	!@ если локация является чекпоинтом, создаём чекпоинт
	checkpoint[$curloc]+=1
	gosub 'avs.save', 999
end
time['goto']=0	& !@ обнуляем переменную перехода. По этой переменной отслеживаем необходимость прокрутки текста на локации
!@ отладочный код:
!@ showstat 1 & clr & pl $replace($maintxt,'<','&lt;')	& !@< отладочный код удаляем из релизной версии
--- onNewLoc ---------------------------------

[костыль 1]
данный костыль должен позволять прокручивать страницу при повторном посещении до низу.
смысл в чём. Элемент добавляется только тогда, когда страница посещена первый раз.
Если же элемент не найден, скрипт прокрутит полосу прокрутки до низа.

[костыль 2]
данный костыль скорее всего был нужен, чтобы убить временные переменные типа lvar до автосохранения,
чтобы при загрузке чекпоинта значения этих переменных не восстанавливались.
