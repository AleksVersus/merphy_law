QSP-Game ! локация, выписывающая готовый текст в таблицу экрана.
# int.screen
args[9]=args[9]	& !@ инициализируем аргументы
:заголовок
if $args[2]='' and instr($curloc,'page')<>0:
	!@ если заголовок не передавали, то он извлекается из идентификатора локации
	$args['head']=$strfind($curloc,'\d+')
else
	!@ в противном случае заголовком становится переданный заголовок
	$args['head']=$args[2]
end
if $args[4]='' and instr($curloc,'page')<>0:
	!@ если колонтитул не передан, извлекаем его из идентификатора локации
	$args['colontitle']=$strfind($curloc,'\d+')
else
	!@ в противном случае колонтитул берётся из переданного аргумента
	$args['colontitle']=$args[4]
end
if $args['colontitle']<>'' and $args['colontitle']<>'nope':
	!@ если колонтитул получен, переформируем его
	$args['colontitle']='-'+$args['colontitle']+'-'
	!@ колонтитул оборачивается в стили в зависимости от пллера
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['colontitle']='<span class="avs_text_colontitle">'+$args['colontitle']+'</span>'
	else
		$args['colontitle']='<font color=#888888 size=1>'+$args['colontitle']+'</font>'
	end
else
	$args['colontitle']=''
end
if instr($args[2],'<checkpoint>')<>0:
	!@ данная локация является контрольной точкой
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['result.head']='<span class="avs_text_head_chp">'+$args['head']+'</span>'
	else
		$args['result.head']='<font color=#994400 size=6><b>'+$args['head']+'</b></font>'
	end
elseif instr($args[2],'<checkpoint>')=0:
	!@ данная локация НЕ является контрольной точкой
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['result.head']='<span class="avs_text_head_smp">'+$args['head']+'</span>'
	else
		$args['result.head']='<font color=#008899 size=6><b>'+$args['head']+'</b></font>'
	end
end
:ссылка_назад
if $GAME_INTERFACE['back.href']='true':
	!@ кнопка, возвращающая назад включена
	$args['hr.b']='<a href="exec:gosub ''back.varriors''" class="plain" id="avs_back_btn">'
	$args['hr.b.n']='Назад'
	$args['/hr.b']="</a>"
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['back.href']=$args['hr.b']+$args['hr.b.n']+$args['/hr.b']
	else
		$args['back.href']=$args['hr.b']+'<font color=#008888><b>'+$args['hr.b.n']+'</b></font>'+$args['/hr.b']
	end
else
	$args['back.href']="&nbsp;"
end
!@ работа с основным описанием
if instr($args[3],'[game]')=1:
	:статы
	!@ статистика упаковывается в общую таблицу
	$args['stat']='<table class="avs_game_stat_short">'
		$args['stat']+='<tr><td align=right>'
			!@ характеристики сила, ловкость,обаяние
			$args['stat']+=$func('base.txt','table.char')
		$args['stat']+='</td></tr>'
		$args['stat']+='<tr name=numbers><td align=right>'
			!@ числа удачи и неудачи
			$args['stat']+=$func('base.txt','table.numbers')
		$args['stat']+='</td></tr>'
		$args['stat']+='<tr name=knew><td align=right>'
			!@ различная информация, полученная в ходе игры
			$args['stat']+=$func('base.txt','table.knew')
		$args['stat']+='</td></tr>'
	$args['stat']+='</table>'
	:предметы
	!@ список предметов тоже генерируем в виде таблицы:
	$args['objs.scroll']=$func('base.txt','table.blast')
	!@ для классического плеера заворачиваем список в дополнительную таблицу
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['objs.scroll']=$args['objs.scroll']
	else
		$args['objs.scroll']="<table cellpadding=0 cellspacing=0><tr><td>"+$args['objs.scroll']+"</td></tr></table>"
	end
	:дополнительные_тексты
	!@ дополнительные тексты
	if arrsize('$print')>0 or arrsize('$print_down')>0:
		!@ не помню, в чём разница между $print и $print_down 
		if arrsize('$print')>0: $args['r']=$func('#array.prnt#','$print','[only result]')
		if arrsize('$print_down')>0:
			if $AVS_PLAYERTYPE['size']="MINI":
				$args['print.down.log']=$func('#array.prnt#','$print_down','[only result]')
			else
				$args['r']+=$func('#array.prnt#','$print_down','[only result]')
			end
		end
		if $AVS_PLAYERTYPE['html_support']='qSpider':
			$args['r']='<span class="avs_text_infoscroll">'+$args['r']+'</span>'
		else
			$args['r']='<font color=#000088>'+$args['r']+'</font>'
		end
		$args['info.scroll']='<table cellspacing=0 width=100%>'
			$args['info.scroll']+='<tr name=printsp bgcolor=#fdfdff><td align=left>&nbsp;</td></tr>'
			$args['info.scroll']+='<tr name=printpr bgcolor=#fdfdff><td align=left>'+$args['r']+'</td></tr>'
		$args['info.scroll']+='</table>'
	else
		$args['info.scroll']='&nbsp;'
	end
	!@ общая таблица из объектов и дополнительных текстов
	if $AVS_PLAYERTYPE['size']="MINI":
		!@ для мобильной версии табличка растягивается на ширину страницы
		$args['objs_n_info']='<table width=100% cellpadding=0 cellspacing=0 border=0>'
	elseif $AVS_PLAYERTYPE['html_support'] = 'qSpider':
		!@ для навигатора и паука поведение таблички определяется стилями CSS
		$args['objs_n_info']='<table class="avs_game_objs_short">'
	else
		!@ для классического плеера ширина таблички равна 280 пикс.
		$args['objs_n_info']='<table cellpadding=0 cellspacing=0 border=0>'
	end
		$args['objs_n_info']+='<tr name=blast><td>'
			$args['objs_n_info']+=$args['objs.scroll']
		$args['objs_n_info']+='</td></tr>'
		$args['objs_n_info']+='<tr name=print><td>'
			$args['objs_n_info']+=$args['info.scroll']
		$args['objs_n_info']+='</td></tr>'
	$args['objs_n_info']+='</table>'
else
!@ не формируем содержимое ячеек с характеристиками и предметами
	$args['stat']='&nbsp;'
	$args['objs_n_info']='&nbsp;'
end
!@ размеры
if $args['info.scroll']='&nbsp;' and $args['objs.scroll']='&nbsp;':
	$args['width.right']='width=5%'
	$args['width.center']=''
else
	$args['width.right']='width=25%'
	$args['width.center']='width=50%'
end
!@ генерируем основное описание и список предметов на локации. Текст экрана и текст предметов засовываем в таблицу
$args['maintxtOp']='<table width=100% border=0>'
	$args['maintxtOp']+='<tr><td name="maintxtOp">'+$args[0]+'<br><br></td></tr>'
	if $args[1]<>'':
		$args['maintxtOp']+='<tr name=objects><td bgcolor=#f0ffff>'+$func('base.txt','note.objLoc')+$args[1]+'</td></tr>'
	end
$args['maintxtOp']+='</table>'
$args[0]=$args['maintxtOp']
if $GAME_INTERFACE['next.href']='true':
	!@ ссылка для пролистывания всех глав по порядку
	$args['hr.np']='<a href="exec:power[''now'']=power[''start''] & $GAME_VALUE[''interface'']=''[game]'' & $GAME_INTERFACE[''refresh'']=''страница.сброс'' & goto ''page.<<$str(val($strfind($curloc,"\d+"))+1)>>''" id="avs_next_btn">'
	$args['/hr.np']='</a>'
	$args['hr.np.n']="next"
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['nextpage']=$args['hr.np']+$args['hr.np.n']+$args['/hr.np']
	else
		$args['nextpage']=$args['hr.np']+'<font color=#008888>'+$args['hr.np.n']+'</font>'+$args['/hr.np']
	end
elseif (instr($args[3],'[game]')=1 or save['count']>0):
	!@ если уже запущен режим игры, т.е. включены боковые панели, или сделано хотя бы одно сохранение
	$args['hr.np.e']='<a href="exec:gosub ''avs.sets''" class="plain" id="avs_epsilon_btn">'
	$args['hr.np.s']='<a href="exec:gosub ''avs.save''" class="plain" id="avs_septima_btn">'
	$args['/hr.np']='</a>'
	if $AVS_PLAYERTYPE['html_support'] = 'qSpider':
		$args['nextpage']+=$args['hr.np.e']+"é"+$args['/hr.np']
		$args['nextpage']+='&nbsp;'
		$args['nextpage']+=$args['hr.np.s']+"S"+$args['/hr.np']
	else
		$args['nextpage']+=$args['hr.np.e']+"<font color=#008888><b>é</b></font>"+$args['/hr.np']
		$args['nextpage']+='&nbsp;'
		$args['nextpage']+=$args['hr.np.s']+"<font color=#008888><b>S</b></font>"+$args['/hr.np']
	end
else
	$args['nextpage']='&nbsp;'
end
!@ непосредственно генерим таблицу
if $AVS_PLAYERTYPE['size']<>"MINI":
	if $AVS_PLAYERTYPE['html_support'] = '':
	!@ для классического плеера генерируем полную таблицу
		$args['result']='<center><table cellpadding=10 border=0 width=100%>'
			if $args['head']<>'':
				!@ заголовок существует
				$args['result']+='<tr name=head>'
					$args['result']+='<td width=25% align=right>'+$args['back.href']+'</td>'
					$args['result']+='<td <<$args["width.center"]>> align=center>'+$args['result.head']+'</td>'
					$args['result']+='<td <<$args["width.right"]>> >'+$args['nextpage']+'</td>'
				$args['result']+='</tr>'
			end
			$args['result']+='<tr name=centr>'
				$args['result']+='<td valign=top align=right width=25%>'+$args['stat']+'</td>'
				$args['result']+='<td <<$args["width.center"]>> valign=top>'+$args[0]+'</td>'
				$args['result']+='<td <<$args["width.right"]>> valign=top align=left>'+$args['objs_n_info']+'</td>'
			$args['result']+='</tr>'
			if $args['colontitle']<>'':
				$args['result']+='<tr>'
					$args['result']+='<td valign=top align=right width=25%>&nbsp;</td>'
					$args['result']+='<td <<$args["width.center"]>> name=btm valign=middle align=center>'+$args['colontitle']+'</td>'
					$args['result']+='<td <<$args["width.right"]>> valign=top align=left>&nbsp;</td>'
				$args['result']+='</tr>'
			end
		$args['result']+='</table></center>'
	elseif $AVS_PLAYERTYPE['html_support'] = "qSpider":
	!@ если Quest Navigator или qSpider, пробуем генерить дивами
		!@ левая колонка
		if $args['stat']<>'' or $args['back.href']<>'':
			$args['result']='<div id="avs_game_left_column">'
				if $args['back.href']<>'': $args['result']+='<div id="avs_game_back">'+$args['back.href']+'</div>'
				if $args['stat']<>'': $args['result']+='<div id="avs_game_statisctics">'+$args['stat']+'</div>'
			$args['result']+='</div>'
		end
		!@ центральная колонка
		$args['result']+='<div id="avs_game_center_column">'
			if $args['result.head']<>'': $args['result']+='<div id="avs_game_head">'+$args['result.head']+'</div>'
			$args['result']+='<div id="avs_game_opinion">'+$args[0]+'</div>'
			if $args['colontitle']<>'': $args['result']+='<div id="avs_game_colontitle">'+$args['colontitle']+'</div>'
		$args['result']+='</div>'
		!@ правая колонка
		$args['result']+='<div id="avs_game_right_column">'
			if $args['nextpage']<>'': $args['result']+='<div id="avs_game_sets">'+$args['nextpage']+'</div>'
			if $args['objs_n_info']<>'': $args['result']+='<div id="avs_game_objects">'+$args['objs_n_info']+'</div>'
		$args['result']+='</div>'
		$args['result']='<div id="avs_main_screen">'+$args['result']+'</div>'
	end
else
	!@ Здесь моделируется интерфейс для приложений на андроид или с маленькими экранами
	$args['back.to.page']='<a href="exec:$GAME_VALUE[''screen.floor'']='''' & gs ''true.goto''" id="avs_backtopage_btn">'
	$args['back.to.up']='<a href="exec:gs ''true.goto''" id="avs_backtoup_btn">'
	$args['open.stat']='<a href="exec:$GAME_VALUE[''screen.floor'']=''STATS'' & gs ''true.goto''" id="avs_openstat_btn">'
	$args['open.objs']='<a href="exec:$GAME_VALUE[''screen.floor'']=''OBJS'' & gs ''true.goto''" id="avs_openobjs_btn">'
	if $AVS_PLAYERTYPE['html_support'] = '':
		!@ классический плеер. Может быть форк для андроид
		if arrsize('$print')>0:
			!@ описательные моменты выводятся поверх всех прочих текстов
			$args['result']+=$args['info.scroll']
			$args['result']+='<br><font color=#0000ff>[</font>'+$args['back.to.up']+'Назад</a><font color=#0000ff>]</font><br>'		
		else
			!@ если это не описательный момент, смотрим по значению переменной, отмечающей состояние экрана
			if $args['back.href']<>"&nbsp;":
				if $GAME_VALUE['screen.floor']='STATS' or $GAME_VALUE['screen.floor']='OBJS':
					!@ если открыта статистика, или предметы, выводим кнопку ЗАКРЫТЬ
					$args['tools']+='<font color=#0000ff>[</font>'+$args['back.to.page']+'Закрыть</a><font color=#0000ff>]</font> &nbsp; &nbsp; '
				else
					!@ если ни предметы, ни статистика не открыты, выводим кнопку НАЗАД
					$args['tools']+='<font color=#0000ff>[</font>'+$args['back.href']+'<font color=#0000ff>]</font> &nbsp; &nbsp; '
				end
				if $GAME_VALUE['screen.floor']='STATS':
					!@ если открыта статистика, ссылка неактивна
					$args['tools']+='<font color=#888888>[<<fast["now"]>>|<<power["now"]>>|<<social["now"]>>]</font> &nbsp; &nbsp; '
				else
					$args['tools']+='<font color=#0000ff>[</font>'+$args['open.stat']+'<<fast["now"]>>|<<power["now"]>>|<<social["now"]>></a><font color=#0000ff>]</font> &nbsp; &nbsp; '
				end
				if $GAME_VALUE['screen.floor']='OBJS':
					!@ если открыты предметы, ссылка не активна
					$args['tools']+='<font color=#888888>[Предметы]</font>'
				else
					$args['tools']+='<font color=#0000ff>[</font>'+$args['open.objs']+'Предметы</a><font color=#0000ff>]</font>'
				end
			end
			if $GAME_VALUE['screen.floor']='STATS':
				!@ при включенной статистике выводим статы
				$args['result']+='<b>'+$args['tools']+'</b>'+'<br><br>'
				$args['result']+=$args['stat']
				$args['result']+='<br><br>'+'<b>'+$args['tools']+'</b>'
				$args['result']+='<h2><a href="exec:gosub ''avs.save'',6">Быстрое сохранение</a></h2>'
				$args['result']+='<h2><a href="exec:gosub ''avs.save'',7">Быстрая загрузка</a></h2>'
				$args['result']+='<h2><a href="exec:gosub ''avs.save'',4">Загрузить контрольную точку</a></h2>'
			elseif $GAME_VALUE['screen.floor']='OBJS':
				!@ при включенных предметах, выводим предметы
				$args['result']+='<b>'+$args['tools']+'</b>'+'<br><br>'
				$args['result']+=$args['objs.scroll']
				$args['result']+='<br><br>'+'<b>'+$args['tools']+'</b>'
			else
				!@ стандартный экран, т.е. сами локации
				if $args['back.href']<>"&nbsp;":$args['result']+='<b>'+$args['tools']+'</b>'
				$args['result']+='<h2>'+$args['result.head']+'</h2>'
				$args['result']+=$args[0]
				$args['result']+=$args['print.down.log']
				$args['result']+=$args['colontitle']
				if $args['back.href']<>"&nbsp;":$args['result']+='<br>'+'<b>'+$args['tools']+'</b>'
			end
		end
	elseif $AVS_PLAYERTYPE['html_support'] = "qSpider":
		!@ для плееров версии quest navigator и qSpider
		if arrsize('$print')>0:
			!@ описательные моменты выводятся поверх всех прочих текстов
			$args['result']+=$args['info.scroll']
			$args['result']+='<span class="avs_text_minibtn">['+$args['back.to.up']+'Назад</a>]</span><br>'		
		else
			!@ если это не описательный момент, смотрим по значению переменной, отмечающей состояние экрана
			if $args['back.href']<>"&nbsp;":
				if $GAME_VALUE['screen.floor']='STATS' or $GAME_VALUE['screen.floor']='OBJS':
					!@ если открыта статистика, или предметы, выводим кнопку ЗАКРЫТЬ
					$args['tools']+='<span class="avs_text_minibtn">['+$args['back.to.page']+'Закрыть</a>]</span> &nbsp; &nbsp; '
				else
					!@ если ни предметы, ни статистика не открыты, выводим кнопку НАЗАД
					$args['tools']+='<span class="avs_text_minibtn">['+$args['back.href']+']</span> &nbsp; &nbsp; '
				end
				if $GAME_VALUE['screen.floor']='STATS':
					!@ если открыта статистика, ссылка неактивна
					$args['tools']+='<span class="avs_text_minibtn_disactive">[<<fast["now"]>>|<<power["now"]>>|<<social["now"]>>]</span> &nbsp; &nbsp; '
				else
					$args['tools']+='<span class="avs_text_minibtn">['+$args['open.stat']+'<<fast["now"]>>|<<power["now"]>>|<<social["now"]>></a>]</span> &nbsp; &nbsp; '
				end
				if $GAME_VALUE['screen.floor']='OBJS':
					!@ если открыты предметы, ссылка не активна
					$args['tools']+='<span class="avs_text_minibtn_disactive">[Предметы]</span>'
				else
					$args['tools']+='<span class="avs_text_minibtn">['+$args['open.objs']+'Предметы</a>]</span>'
				end
			end
			if $GAME_VALUE['screen.floor']='STATS':
				!@ при включенной статистике выводим статы
				$args['result']+=$args['tools']+'<br><br>'
				$args['result']+=$args['stat']
				$args['result']+='<br><br>'+$args['tools']
				$args['result']+='<h2><a href="exec:gosub ''avs.save'',6">Быстрое сохранение</a></h2>'
				$args['result']+='<h2><a href="exec:gosub ''avs.save'',7">Быстрая загрузка</a></h2>'
				$args['result']+='<h2><a href="exec:gosub ''avs.save'',4">Загрузить контрольную точку</a></h2>'
			elseif $GAME_VALUE['screen.floor']='OBJS':
				!@ при включенных предметах, выводим предметы
				$args['result']+=$args['tools']+'<br><br>'
				$args['result']+=$args['objs.scroll']
				$args['result']+='<br><br>'+$args['tools']
			else
				!@ стандартный экран, т.е. сами локации
				if $args['back.href']<>"&nbsp;":$args['result']+=$args['tools']
				$args['result']+='<h2>'+$args['result.head']+'</h2>'
				$args['result']+=$args[0]
				$args['result']+=$args['print.down.log']
				$args['result']+=$args['colontitle']
				if $args['back.href']<>"&nbsp;":$args['result']+='<br>'+$args['tools']
			end
		end
	end
end
killvar '$print'
killvar '$print_down'
$result=$args['result']
--- int.screen ---------------------------------
АРГУМЕНТЫ:
$args[0] = $args[0]	&	!	текст экрана
$args[1] = $args[1]	&	!	список предметов
$args[2] = $args[2]	&	!	заголовок
$args[3] = $args[3]	&	!	управление
$args[4] = $args[4]	&	!	колонтитул
